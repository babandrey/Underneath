if {Fear.met} == false:
	jump First Meeting
if {Fear.quest_taken} == false:
	jump Quest
elif {Fear.quest_completed} == false:
	jump Waiting
else:
	if {Fear.is_colored} == false:
		jump Quest Completed
	else:
		jump Colored

# --------------------------------------------------------------------------------------------------------------------------------
label First Meeting

[i]You notice an [b]oddly suspicious urn[/b]...[pause=0.5][br]Just standing there..[pause=0.5] menacingly.[/i]
[i]On closer inspection... it seems to be shaking just ever so slightly...[/i]
- Knock on the Suspicious Urn
- Ignore it
	[end_timeline]

set {Fear.met} = true
[signal arg="open"]
[i]The lid of the urn starts lifting slowly...[pause=0.5][br]As a large form slithers from inside it.[/i]

Unknown: Y-Ye[i]sss[/i]?
- Why are you in this urn?
Unknown: I don't know...[pause=0.5] It used to give me...[pause=0.5] [b]sssafety[/b]..[pause=0.5]\
Gave me [b]comfort[/b]...[pause=0.5] I guess.
Unknown: Now the only thing I have is this [b]mask[/b].
jump Quest

# --------------------------------------------------------------------------------------------------------------------------------
label Quest

if {Fear.times_nudged} == 0:
	Unknown: It's better being in this [b]urn[/b] than walking outside.[br][pause=0.5]You can never know what might happen outside..
	- Isn't it dark in there?
	Unknown: Not realllly..[pause=0.5] Spending so much time inside, gave me excellent [b]night vision[/b].
	- Are you not claustrophobic?
	Unknown: No...[pause=0.5] It's actually pretty comforting inside..
	- What if the urn would break?
	Unknown: I don't know...[pause=0.5] I didn't really think about it.
	- Is there anything else inside?
	Unknown: I [i]sss[/i]ure hope not...
	Unknown: I'm going back inssside...[pause=0.5][br]Good luck out there.
	
	[signal arg="close"]
	[i]The massive form slowly slides back inside the urn, as the lid fits back on top of it...[pause=0.5][br]Almost too perfectly.[/i]
	set {Fear.times_nudged} += 1
	[end_timeline]

[i] There it is...[pause=0.5] the [b]Suspicious Urn[/b],[pause=0.5] still moving slightly to the movements of whoever is inside it.[/i]
- Knock on the Suspicious Urn
[i] You wonder if it will ever leave the safety of it's urn again...[/i]
[i] Maybe there's something you can do to [b]elicit[/b] such a response..[/i]
set {Fear.quest_taken} = true
[end_timeline]

# --------------------------------------------------------------------------------------------------------------------------------
label Waiting
[i]What could ever get[pause=0.5] whoever is inside this [b]Suspcious Urn[/b][pause=0.5] to come out again?[/i]
[end_timeline]

# --------------------------------------------------------------------------------------------------------------------------------
label Quest Completed
[i]There it is...[pause=0.5] The [b]Suspicious Urn[/b][pause=0.5].[pause=0.5].[pause=0.5].[pause=1][/i]
- Open the lid and throw the Spider in

[signal arg="open"]
Unknown: [speed=0][b]AAAAAAAAAAAAAAAAAAH!!!!![/b][/speed]
[i]The entity emerges quickly from its urn, panting heavily...[pause=1][br]It's not like you [b]scared[/b] it or anything....[/i]
[signal arg_type="dict" arg="{"Fear":"complete_quest"}"]
set {Fear.is_colored} = true
[wait time="3.0"]
Unknown: WHY would you do this????
- Calm down, it's ok!
Unknown: No it's NOT!!
- The spider went away! You're safe
Unknown: A-Are you s-sure??
[i]The entity looks around nervously,[pause=0.5] searching for the spider.[/i]
fear: I, [pause=0.5]am the [b][color=#a963a7]Avatar of Fear[/color][/b]. [pause=0.5][br]You sure did scare the hell out of me... [pause=0.5] scared the color right back into me.
set {Fear.quest_completed} = true

jump Colored
# --------------------------------------------------------------------------------------------------------------------------------
label Colored

if {Fear.relationship} == 0:
	fear: W-Who a-are you?
	if {player_name} == "":
		[text_input text="Enter your name:" var="player_name"]
	else:
		- {player_name}
	fear: W-What are you doing h-here, [color=#a963a7]{player_name}[/color]?
	- Just wandering around..
	- Just trying to help..
	- I'm lost..
	set {Fear.relationship} += 1
	fear: Then w-why would you scare me??
	- Restoring color to the world
		fear: Y-you...[pause=0.5] are right.[br]I did lose myself.
		fear: Now that [b]fear[/b] is restored, everyone can be careful of real danger.
		set {Fear.relationship} += 1
		jump Colored
	
	- Just for fun
		fear: M-Mean....
		[signal arg="close"]
		[i]Fear slides back into his urn. Fearful you might do something scary again.[/i]
		[end_timeline]

elif {Fear.relationship} == 1.0:
	[i]...[/i]
	- Knock on the Urn
	[signal arg="peak_open"]
	[i]Fear opens the lid slightly, just enough so fear can peek outside.[/i]
	fear: H-Huh?
	- I'm sorry
	- Don't worry, I'm not here to scare you..
		fear: A-Are you s-sure??
		[i]Fear looks around nervously...[/i]
		- Yes, I promise
		- It's your call...
			[signal arg="peak_closed"]
			[i]The lid closes as Fear retracts back into his urn.[/i]
			[end_timeline]
	[signal arg="open"]
	[i]Fear comes out of the urn again.[/i]
	fear: O-okay. I trust you..
	set {Fear.relationship} += 1
	jump Colored

elif {Fear.relationship} == 2:
	fear: S-Say,[pause=0.5] can I ask you a q-question?
	- Yes
		fear: Are you afraid of anything?
		- The Ocean
			fear: [color=#a963a7][b]Thalassophobia[/b][/color].[pause=1][br][b]D-Drowning[/b], [b]The Abyss[/b] and [b]Giant Underwater Monsters[/b].[pause=1] Very scary.
			fear: Lucky there are no oceans here...
		- Snakes
			fear: [color=#a963a7][b]Ophidiophobia[/b][/color].[pause=1][br]Snakes are excellent at [b]camouflage[/b]. Can [b]Strike out of nowhere[/b]. And let's not talk about the [b]Venom[/b].
			fear: Waaait a minute[pause=0.5].[pause=0.5].[pause=0.5]D-Do I scare you??
			- Yes
				fear: I'm s-sorry.[pause=0.5] I mean you no harm.
			- No
				fear: [i]*phew*[/i] Glad to hear that.
		- Spiders
			fear: [color=#a963a7][b]Arachnophobia[/b][/color].[pause=1][br]The skittering creature. You can always find one,[pause=0.5] no matter where you are.
			fear: Hheeeeeyyyy...[pause=1] How did you bring that spider then?!
			- I did it for you
				fear: O-Okay.. Free exposure therapy then.
			- Spider? WHERE?!
				fear: No no! There are no spiders! Calm down!
				- *phew* Ok...
		- The Dark
			fear: [color=#a963a7][b]Nyctophobia[/b][/color].[pause=1][br]Scary things can hide in the shadows...[pause=0.5][br]Lucky I can [b]see in the dark[/b]..
		- I'm not afraid
			fear: Odd...[pause=0.5] Must be something you're afraid of...
			fear: Maybe you are [b][i]scared[/i][/b] to admit it...
	- No
		fear: Oh...
		[end_timeline]
	
	set {Fear.relationship} += 1
	jump Colored
elif {Fear.relationship} == 3:
	fear: Thanks for not [b]scaring[/b] me again.
	fear: It's been nice talking about our [b]fears[/b].
	[i]Fear pulls out a small hammer from the urn and very swiftly...[pause=0.5][br][b]taps your mask with it[/b]..[pause=0.5] Cracking it slightly.[/i]
	
	fear: I've granted you my [b]Night Vision[/b].[pause=0.5] So you will feel safe, even in the darkest of places.
	[signal arg="close"]
	[i]Fear slides back into the urn, the lid closing it behind again.[/i]
	set {Fear.relationship} = -1
	[signal arg_type="dict" arg="{"Fear":"break_player_mask"}"]
	[end_timeline]
elif {Fear.relationship} == -1:
	[signal arg="peak_open"]
	[i]The lid opens just enough for Fear to peek outside.[/i]
	fear: H-Huh?
	- Talk about fears again
		jump Fears
	- End Conversation
		[signal arg="peak_closed"]
		[end_timeline]

# --------------------------------------------------------------------------------------------------------------------------------
label Fears

fear: Are you afraid of anything?
- The Ocean
	fear: [color=#a963a7][b]Thalassophobia[/b][/color].[pause=1][br][b]D-Drowning[/b], [b]The Abyss[/b] and [b]Giant Underwater Monsters[/b].[pause=1] Very scary.
	fear: Lucky there are no oceans here...
	jump Fears
- Snakes
	fear: [color=#a963a7][b]Ophidiophobia[/b][/color].[pause=1][br]Snakes are excellent at [b]camouflage[/b]. Can [b]Strike out of nowhere[/b]. And let's not talk about the [b]Venom[/b].
	fear: Waaait a minute[pause=0.5].[pause=0.5].[pause=0.5]D-Do I scare you??
	- Yes
		fear: I'm s-sorry.[pause=0.5] I mean you no harm.
	- No
		fear: [i]*phew*[/i] Glad to hear that.
	jump Fears
- Spiders
	fear: [color=#a963a7][b]Arachnophobia[/b][/color].[pause=1][br]The skittering creature. You can always find one,[pause=0.5] no matter where you are.
	fear: Hheeeeeyyyy...[pause=1] How did you bring that spider then?!
	- I did it for you
		fear: O-Okay.. Free exposure therapy then.
	- Spider? WHERE?!
		fear: No no! There are no spiders! Calm down!
		- *phew* Ok...
	jump Fears
- I'm not afraid
	fear: Odd...[pause=0.5] Must be something you're afraid of...
	fear: Maybe you are [b][i]scared[/i][/b] to admit it...
	jump Fears
- End Conversation
	jump Colored