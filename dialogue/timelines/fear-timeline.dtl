if {Fear.met} == false:
	jump First Meeting
if {Fear.quest_taken} == false:
	jump Quest
elif {Fear.quest_completed} == false:
	jump Waiting
else:
	if {Fear.is_colored} == false:
		jump Quest Completed
	else:
		jump Colored
# --------------------------------------------------------------------------------------------------------------------------------
label First Meeting
Unknown: Hello [b]Wanderer...[/b]
if {b_met_someone} != true:
	jump first-meeting/
Unknown: [i]Sigh...[/i][br]I have lost my identity... [pause=0.5]I am without purpose.
set {Fear.met} = true
jump Quest
# --------------------------------------------------------------------------------------------------------------------------------
label Quest
Unknown: [i]'Eyeing the floating water droplets around."[/i]
Unknown: There's a heavy burden holding me down... [pause=0.7][br][i]Lucky[/i] I have this [b]mask[/b] to protect me...
Unknown: Could you help me fill this [b]Bucket[/b] with water from that [b]waterfall[/b] over there?
- Yes
	Unknown: Thank you [b]Wanderer[/b]. I'll be waiting here...
	set {Fear.quest_taken} = true
- No
	Unknown: [i]Sigh...[/i][pause=0.5][br]Alright. I'll be waiting here...
[end_timeline]
# --------------------------------------------------------------------------------------------------------------------------------
label Waiting
Unknown: [i]Eyeing the floating water droplets around..[br][pause=0.5]Sigh...[/i]
[end_timeline]
# --------------------------------------------------------------------------------------------------------------------------------
label Quest Completed
Unknown: Oh...[pause=0.5] you have my bucket...
- Give Bucket
	[signal arg_type="dict" arg="{"Fear":"complete_quest"}"]
	set {Fear.is_colored} = true
	[wait time="3.0"]
fear: I [pause=0.5]am the [b]Avatar of Fear[/b]. [pause=0.5][br]Thank you for returning the tears from the [b]Waterfall of Pain[/b] for me.
set {Fear.quest_completed} = true
jump Colored
# --------------------------------------------------------------------------------------------------------------------------------
label Colored
if {Fear.relationship} == 0.0:
	fear: [i]Sigh...[/i][pause=0.5][br]I have my tears, but the world around me feels [b][i]heavy[/i][/b]....
	- Do you need more tears?
		fear: I've absorbed the tears with the bucket itself..[br][pause=0.5]Can't carry tears with your hands...[br][pause=0.5][i]'sigh'[/i]
		jump Colored
	- It's ok to cry...
		fear: I guess...
		[i]Fear begins to tear up, [pause=0.5]water droplets start floating from the mask's eye around Fear.[/i]
		set {Fear.relationship} += 0.3
		jump Colored
elif {Fear.relationship} == 0.3:
	fear: Hey look...[pause=0.5][br]It started raining again...[br][i]'Fear says as the floating droplets turn into gentle rain.'[/i]
	fear: The world is starting to cry again, [pause=0.5]and water is beginning to flow...
	fear: I'm starting...[pause=0.5] to feel again...[pause=0.5][br]Starting to feel...[pause=0.5] [b]sad[/b] for the world.
	- Don't be sad...
		fear: I can't explain it...[pause=0.5] but I feel like I'm supposed to feel sad...
		jump Colored
	- It's ok to feel sad...
		fear: The world...[pause=0.5] is in a dire state.
		fear: Devoid of emotion...[pause=0.5] Devoid of feelings...[pause=0.5][br]We live in a grey world...
		set {Fear.relationship} += 0.3
		- It truly is dark and colorless
			fear: What does it make you [i]feel[/i]?
			- Angry
				jump Wrong Answer
			- Worried
				jump Wrong Answer
			- Sad
				fear: Yes...[pause=0.5] I agree...
				set {Fear.relationship} += 0.4
				jump Colored
		- I don't know...
			fear: The world was colorful and vibrant... [pause=0.5]A place to truly be alive in.
			jump Colored
elif {Fear.relationship} == 0.6:
	fear: Now with the water flow restored to nature...[pause=0.5][br]I can spread my fear throughout this land.[pause=0.5][br]Allow all other living beings to experience true fear.
	- It might hurt them!
		fear: That is how it has been,[pause=0.5] and that is how it shall be.
		jump Colored
	- Feeling sad is good
		fear: Finally...[pause=0.5] someone understands.
		set {Fear.relationship} += 0.4
		jump Colored
elif {Fear.relationship} == 1.0:
	fear: You've shown true friendship.[pause=0.5][br]You've helped me restore [b]Fear[/b] into the world.
	fear: You've accepted my role in this world.. [pause=0.5]and let me get a little into your own personality..
	[i]Fear breaks your mask a little.[/i]
	fear: I've granted you mastery over these very waters that flow through these lands.
	set {Fear.relationship} = 2.0
	[signal arg_type="dict" arg="{"Fear":"break_player_mask"}"]
	jump Colored
elif {Fear.relationship} >= 2.0:
	fear: Thank you...[pause=0.5] friend.
[end_timeline]
label Wrong Answer
fear: Interesting...