if {Anger.met} == false:
	jump First Meeting
if {Anger.quest_taken} == false:
	jump Quest
elif {Anger.quest_completed} == false:
	jump Waiting
else:
	if {Anger.is_colored} == false:
		jump Quest Completed
	else:
		jump Colored

# --------------------------------------------------------------------------------------------------------------------------------
label First Meeting
if {Anger.times_nudged} == 0:
	[i]What or whoever it is, seems to be actively ignoring your presence.[/i]
	set {Anger.times_nudged} = 1
	[end_timeline]

set {Anger.met} = true
set {Anger.times_nudged} = 0
Unknown: What?
- Who are you?
	Unknown: I don't know.[br][pause=0.5]And even if I did, I wouldn't just go around and tell it.
- What is this place?
	Unknown: If you don't know, maybe you don't belong here in the first place.
[end_timeline]

# --------------------------------------------------------------------------------------------------------------------------------
label Quest
Unknown: Listen, I don't know anything.[pause=0.5][br]So please stop bothering me.
Unknown: I feel like I'm [b]about to explode[/b], and your continued pestering ain't helping.
Unknown: Could you please leave me alone until I manage to figure things out??
- Yes
	Unknown: Pfft...
	set {Anger.quest_taken} = true
- No
	Unknown: Fine.
[end_timeline]

# --------------------------------------------------------------------------------------------------------------------------------
label Waiting
[i]What or whoever this rude entity is, it's actively ignoring you.[/i][br][pause=0.5][i]Until it figures itself out...[pause=0.5] maybe you could [b]'assist'[/b] it in finding out...[/i]
[end_timeline]

# --------------------------------------------------------------------------------------------------------------------------------
label Quest Completed
[i]You slowly raise the stick you found..[/i]
- Poke ??? with the stick.. gently.
	[i] You poke ??? with the stick gently... [pause=0.5]It seems to be unphased.[/i]
	- Poke ??? with the stick.. again.
		[i] You poke ??? with the stick again... [pause=0.5]It sighs dominantely,[pause=0.5] still ignoring your presence.[/i]
		- Poke ??? with the stick.. repeatedly.
			[i] After a couple more prods.. the entity grabs the stick from your hand and...[br][pause=0.5]breaks it into pieces.[br]He then proceeds to turn red out of [b]Anger[pause=0.2].[pause=0.2].[pause=0.2].[/b]
[signal arg_type="dict" arg="{"Anger":"complete_quest"}"]
set {Anger.is_colored} = true
[wait time="3.0"]
anger: You!	
anger: I, [pause=0.5]am the [b][color=#c54511]Avatar of Anger[/color][/b]. [pause=0.5][br]How dare you disturb me needlessly!
- I helped restore your emotion!!
	anger: Fine. I'll let it slide...[pause=0.5] This time.
set {Anger.quest_completed} = true
[end_timeline]

# --------------------------------------------------------------------------------------------------------------------------------
label Colored
if {Anger.relationship} == 0:
	[i]Anger rolls his eyes the moment he sees you approaching him...[/i]
	anger: What is it... this time? Who even are you??
	if {player_name} == "":
		[text_input text="Enter your name:" var="player_name"]
	else:
		- {player_name}
	anger: And what is it that you want, "[color=#c54511]{player_name}[/color]"?
	- We need to spread Anger into the world.
		anger: "We",[pause=0.5] don't need to spread anything.
	- I have some more questions.
		anger: Good luck finding the answers.
	set {Anger.relationship} += 1
	[end_timeline]
elif {Anger.relationship} == 1:
	anger: Again...[pause=0.5] Stop.[pause=0.5] Bothering.[pause=0.5] Me.
	- I'm just trying to restore this place.
		anger: Well so am I. Now leave me alone.
	set {Anger.relationship} += 1
	[end_timeline]
elif {Anger.relationship} == 2:
	anger: Why,[pause=0.5] out of all places...[pause=0.5] and out of all the times possible..[pause=0.5][br]Must you pester me now? 
	anger: I am NOT going to answer your questions!
	anger: I am NOT part of any "team"!
	anger: And I do NOT need your help!
	anger: Just leave me alone already!
	set {Anger.relationship} += 1
	[end_timeline]
elif {Anger.relationship} == 3:
	[i]Anger stares at you with disbelief.[/i]
	- Hey! I am trying to help here!
		[i]You can see the rage...[pause=0.5] building slowly up inside him as you finish your sentence.[/i]
		- I COME HERE, to an UNKNOWN location
			- And I am stuck here, same as you!
				- I have done NOTHING
					- HELP ME, and I will leave this place!
	[i]Anger exhales loudly...[/i]
	anger: Fine. [pause=0.5]I will let you experience pure [b]Anger[/b].[br]I will allow you to incoporate [b]anger[/b] into your [b]personality[/b].
	[i]Anger pulls out a small hammer and swiftly...[pause=0.5][br][b]taps your mask with it[/b]..[pause=0.5] Cracking it slightly.[/i]
	anger: I've granted you the ability to possess incredible strength when angry.[br][pause=0.5]Use it to break anything blocking your path.
	set {Anger.relationship} = -1
	[signal arg_type="dict" arg="{"Anger":"break_player_mask"}"]
	[end_timeline]
elif {Anger.relationship} == -1:
	anger: Yes?
[end_timeline]